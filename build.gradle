buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.0.1"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.0"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "com.android.application"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 14
        targetSdkVersion 21

        resConfigs "nodpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"
        resConfigs "en"
    }

    packagingOptions {
        exclude "META-INF/services/javax.annotation.processing.Processor"
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        mavenCentral()
    }

    compile "com.android.support:appcompat-v7:21.0.3"
    compile "com.android.support:cardview-v7:21.0.3"
    compile "com.android.support:support-annotations:21.0.3"
    compile "com.f2prateek.bundler:bundler:1.0.0"
    compile "com.f2prateek.dart:dart:1.1.0"
    compile "com.jakewharton:butterknife:6.1.0"
    compile "com.github.frankiesardo:icepick:2.3.6"
    compile "com.google.android.gms:play-services-fitness:6.5.87"
    compile "com.melnykov:floatingactionbutton:1.2.0"
    compile "com.squareup:otto:1.3.6"

    provided "com.github.frankiesardo:icepick-processor:2.3.6"
}

task generateWrapper(type: Wrapper) {
    description "Generates Gradle wrapper."

    gradleVersion = "2.2.1"
}
